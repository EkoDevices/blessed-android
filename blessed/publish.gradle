apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.ekodevices.library'
def LIB_ARTIFACT_ID = 'blessed'
def LIB_VERSION = '1.0.1'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "EkoDevices"
            url = uri("https://maven.pkg.github.com/ekodevices/internal-eko-android-bluetooth-sdk")
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
        }
        maven {
            name = "EkoPartners"
            url = uri("https://maven.pkg.github.com/EkoPartners/eko_connect_android")
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
        }
        maven {
            name = 'LocalFileSystem'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        ekosdk(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/${artifactId}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}